/**
 * Copyright (c) 2014-present, Facebook, Inc. All rights reserved.
 *
 * You are hereby granted a non-exclusive, worldwide, royalty-free license to use,
 * copy, modify, and distribute this software in source code or binary form for use
 * in connection with the web services and APIs provided by Facebook.
 *
 * As with any software that integrates with the Facebook platform, your use of
 * this software is subject to the Facebook Developer Principles and Policies
 * [http://developers.facebook.com/policy/]. This copyright notice shall be
 * included in all copies or substantial portions of the software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
 * FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
 * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
 * IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
 * CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

project.group 'com.facebook.android'

project.ext.name = 'Facebook-Common-Android-SDK'
project.ext.artifactId = "facebook-common"
project.ext.description = 'Facebook Common Android SDK'
project.ext.url = 'https://github.com/facebook/facebook-android-sdk'

dependencies {
    def kotlin_ver = project.ext.kotlinVersion
    // Facebook Dependencies
    api project(':facebook-core')
    // Support Dependencies
    api "androidx.legacy:legacy-support-v4:${project.ext.androidxLegacyVersion}"
    implementation "androidx.appcompat:appcompat:${project.ext.androidxVersion}"
    implementation "androidx.cardview:cardview:${project.ext.androidxCardviewVersion}"
    implementation "androidx.browser:browser:${project.ext.androidxBrowserVersion}"
    implementation("androidx.activity:activity:${project.ext.androidxActivityVersion}")
    implementation("androidx.fragment:fragment:${project.ext.androidxFragmentVersion}")

    // Third-party Dependencies
    implementation 'com.google.zxing:core:3.3.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_ver"

    // Unit Tests
    testImplementation project(":facebook-testutil")
    testImplementation "junit:junit:$junitVersion"
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'androidx.test:core:1.0.0'

    testImplementation "org.mockito:mockito-inline:2.26.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    def powerMockVersion = '2.0.2'
    testImplementation "org.powermock:powermock-core:$powerMockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powerMockVersion"
    testImplementation "org.powermock:powermock-module-junit4:$powerMockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powerMockVersion"
    testImplementation "org.powermock:powermock-classloading-xstream:$powerMockVersion"
    testImplementation 'org.assertj:assertj-core:3.15.0'


    testImplementation "com.android.installreferrer:installreferrer:1.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_ver"
}

android {
    compileSdkVersion project.ext.compileSdk
    buildToolsVersion project.ext.buildTools
    // The version of Jacoco used by the android gradle plugin
    jacoco {
        version "0.8.7"
    }

    defaultConfig {
        minSdkVersion project.ext.minSdk
        targetSdkVersion project.ext.targetSdk
        consumerProguardFiles 'proguard-rules.pro'
        vectorDrawables.useSupportLibrary = true
    }

    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        unitTests.all {
            jvmArgs '-XX:MaxPermSize=1024m'
            maxHeapSize = "4096m"
        }
        unitTests.includeAndroidResources true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    if (System.getenv("SANDCASTLE") == "1") {
        testOptions {
            unitTests.all {
                systemProperty 'robolectric.dependency.repo.url', 'https://maven.thefacebook.com/nexus/content/repositories/central/'
                systemProperty 'robolectric.dependency.repo.id', 'central'
                systemProperty "java.net.preferIPv6Addresses", "true"
                systemProperty "java.net.preferIPv4Stack", "false"
                jacoco {
                    enabled = true
                    includes = []
                    excludes = []
                }
            }
        }
    }
}

if (file("${rootDir}/internal/safekit-build.gradle").exists()) {
    project.apply from: "${rootDir}/internal/safekit-build.gradle"
}

apply from: "${rootDir}/jacoco.gradle"
apply from: "${rootDir}/maven.gradle"
